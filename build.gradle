plugins {
    id 'groovy'
    id 'java-library'
    id 'idea'
    id 'checkstyle'
}

checkstyle {
    toolVersion = '10.12.0' // Use latest stable version
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml") // Path to your custom XML
    ignoreFailures = false // Set to true if you don't want the build to fail on warnings
    showViolations = true  // Show errors in the console
}

group 'io.deephaven'
version "1.5-${dhcVersion}"

allprojects {
    repositories {
        mavenCentral()
    }
}

tasks.wrapper {
    Wrapper w ->
        w.gradleVersion = '8.9'
        w.distributionType = 'ALL'
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {

    implementation platform("io.deephaven:deephaven-bom:${dhcVersion}"),
                   "io.deephaven:deephaven-engine-table",
                   "it.unimi.dsi:fastutil:8.5.13"
    runtimeOnly "io.deephaven:deephaven-log-to-slf4j",
                'ch.qos.logback:logback-classic:1.4.12'
//    testImplementation("io.deephaven:deephaven-engine:0.36.1")
    testImplementation("io.deephaven:deephaven-engine-table:0.36.1")
    testImplementation("io.deephaven:deephaven-engine-test-utils:0.36.1")

}

jar {
    manifest {
        attributes 'Implementation-Title': 'deephaven-pricebook',
                'Implementation-Version': version,
                'Provider': 'gradle',
                'VCS-Branch': "${ -> ext.vcsRevision = 'git rev-parse --abbrev-ref HEAD'.execute(null, rootDir).text.trim()}",
                'VCS-Revision': "${ -> ext.vcsBranch = 'git rev-list --max-count=1 HEAD'.execute(null, rootDir).text.trim()}",
                'Build-Date' : new Date()
    }
}